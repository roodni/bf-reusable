import "./lib.bfr"

main {
  counter: array(_) {
    n: cell;
    i: index;
  };
} [
  $let gen_counter_incr counter = [
    $let i = counter@i in
    $let n = i:n in
    > i
    ! i [
      ? n [] [ + n 10 ]
      - n
      ? n
        [ *gen_reset_index i ]
        [ + n 10  > i ]
    ]
  ] in

  $let gen_counter_print counter = [
    $let i = counter@i in
    $dive i [
      ! i:(1)n [ > i ]
      ! i:n
        [ $alloc { x: cell; } in
          + x ('9' + 1)
          *gen_add_multi i:n x (-1)
          . x
          + i:n ('9' + 1)
          *gen_add_multi x i:n (-1)
          < i
        ]
    ]
  ] in

  $alloc {
    n: cell;
    fizz: cell;
    buzz: cell;
  } in

  *gen_input n '\n'
  + fizz 3
  + buzz 5
  ! n [
    - n
    *gen_counter_incr counter
    $alloc { fb: cell; } in
    + fb 2
    - fizz
    ? fizz [ - fb ] [ + fizz 3  *gen_puts "Fizz" ]
    - buzz
    ? buzz [ - fb ] [ + buzz 5  *gen_puts "Buzz" ]
    ? fb [] [ *gen_counter_print counter ]
    *gen_puts " "
  ]
]
